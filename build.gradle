buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forgegradle_version}", changing: false
        classpath "org.parchmentmc:librarian:${librarian_version}"
    }
}

subprojects {
    group = 'org.shsts.tinycorelib'
    version = "${minecraft_version}-${mod_version}"

    apply plugin: 'checkstyle'
    apply plugin: 'eclipse'
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'org.parchmentmc.librarian.forgegradle'
    apply plugin: 'maven-publish'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    checkstyle {
        toolVersion = "10.20.1"
    }

    minecraft {
        mappings channel: 'parchment', version: "${parchment_version}-${minecraft_version}"
    }

    dependencies {
        minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE

            include 'META-INF/mods.toml'

            expand 'mod_version': "${version}"
        }
    }

    processTestResources {
        from(sourceSets.test.resources.srcDirs) {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE

            include 'META-INF/mods.toml'

            expand 'mod_version': "${version}"
        }
    }

    jar.finalizedBy('reobfJar')

    publishing {
        repositories {
            maven {
                name = 'shsts'
                url = 'https://www.shsts.moe/m2'
                credentials {
                    username = shstsUser
                    password = shstsPassword
                }
            }
        }
    }
}

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
println "Minecraft: ${minecraft_version}, Forge: ${forge_version}, ForgeGradle: ${forgegradle_version}"
